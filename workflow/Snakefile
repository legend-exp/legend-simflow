# Copyright (C) 2023 Luigi Pertoldi <gipert@pm.me>
#
# This program is free software: you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by the Free
# Software Foundation, either version 3 of the License, or (at your option) any
# later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
# details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

import os
from datetime import datetime
from pathlib import Path
import time

from dbetto import AttrsDict

from legendsimflow import aggregate, nersc, utils

start_time = time.time()

print("INFO: initializing simflow context")
SIMFLOW_CONTEXT = utils.init_simflow_context(config, workflow)
config = SIMFLOW_CONTEXT.config

# it takes a while to generate the list of good HPGes, let's do it at the start
# and cache it for later
print("INFO: generating the list of HPGe detectors valid for modeling")
SIMFLOW_CONTEXT["modelable_hpges"] = aggregate.gen_list_of_all_hpges_valid_for_modeling(
    config
)

print(f"INFO: took {time.time()- start_time:.1f} seconds. Handed it over to Snakemake")


def dvs_ro(x):
    return nersc.dvs_ro(config, x)


wildcard_constraints:
    tier=r"\w+",
    simid=r"[-\w]+",
    jobid=r"\d+",
    runid=r"[-\w]+",
    hpge_detector=r"[VPBC]\w{6}",


onstart:
    print("INFO: starting workflow")

    utils.setup_logdir_link(config, SIMFLOW_CONTEXT.proctime)

    # make sure some packages are initialized before we begin to avoid race conditions
    # https://numba.readthedocs.io/en/stable/developer/caching.html#cache-sharing
    if not workflow.touch and "precompile_pkg" in config:
        print(
            f"INFO: pre-compiling Numba cache (NUMBA_CACHE_DIR={os.environ.get('NUMBA_CACHE_DIR')})"
        )
        for module in config.precompile_pkg:
            shell(f"python -c 'from {module} import *'")


include: "rules/aux.smk"


make_tiers = config.get("make_tiers", ["vtx", "stp", "opt", "hit"])
for tier in make_tiers:

    include: f"rules/{tier}.smk"


def gen_target_all():
    if config.get("simlist", "*") in ("all", "*"):
        if "pdf" in make_tiers:
            return rules.gen_pdf_release.input
        elif "evt" in make_tiers:
            return rules.gen_all_tier_evt.input
        elif "hit" in make_tiers:
            return (
                rules.gen_all_tier_hit.input,
                aggregate.gen_list_of_all_plots_outputs(
                    config, tier="hit", cache=SIMFLOW_CONTEXT.modelable_hpges
                ),
            )
        elif "opt" in make_tiers:
            return rules.gen_all_tier_opt.input
        elif "stp" in make_tiers:
            return (
                rules.gen_all_tier_stp.input,
                aggregate.gen_list_of_all_plots_outputs(
                    config, tier="stp", cache=SIMFLOW_CONTEXT.modelable_hpges
                ),
            )
    else:
        return aggregate.process_simlist(config)


rule all:
    """Default rule.

    Makes all output files of the highest tier specified in the `make_tiers`
    configuration variable (holding a list of tiers) and all the simids listed
    in the `simlist` configuration variable.
    """
    default_target: True
    input:
        gen_target_all(),
